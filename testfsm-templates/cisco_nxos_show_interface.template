Value Required Interface (\S+)
Value MACAddr ([\da-f]{4}\.[\da-f]{4}\.[\da-f]{4})
Value BIA ([\da-f]{4}\.[\da-f]{4}\.[\da-f]{4})
Value Desc (.+)
Value IP (\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}\/\d+)
Value MTU (\d+)
Value Speed (\d+ [MG]b/s)
Value MediaType (.*)
Value Rate (\d+ (minute|seconds))
Value InputRate (\d+)
Value InputPPS (\d+)
Value OutputRate (\d+)
Value OutputPPS (\d+)
Value PacketsIn (\d+)
Value BytesIn (\d+)
Value PacketsOut (\d+)
Value BytesOut (\d+)
Value InputErrors (\d+)
Value OutputErrors (\d+)


Start
  ^${Interface} is up
  ^\s+Hardware.*address: ${MACAddr} -> Continue
  ^.*\(bia ${BIA}
  ^\s+Description: ${Desc}
  ^\s+Internet address is ${IP}
  ^\s+MTU ${MTU} bytes
  ^.*duplex, ${Speed}, -> Continue
  ^.*media type is ${MediaType}
  ^\s+${Rate} input rate ${InputRate} bits/sec, ${InputPPS} packets/sec
  ^\s+${Rate} output rate ${OutputRate} bits/sec, ${OutputPPS} packets/sec
  ^\s+${PacketsIn} input packets\s+${BytesIn} bytes
  ^\s+${InputErrors} input error
  ^\s+${PacketsOut} output packets\s+${BytesOut} bytes
  ^\s+${OutputErrors} output error -> Record
# Last line of mgmt0 interface
  ^\s+\d+ broadcast packets -> Record
# Last line of SVI interface
  ^\s+ucast: -> Record